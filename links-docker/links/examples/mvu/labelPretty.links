open import MvuHTML;
open import MvuAttrs;
open import Mvu;

typename Model = (contents: String);
typename Message = [| UpdateBox: String |];

fun reverseString(str) { implode(reverse(explode(str))) }

sig updt : (Message, Model) ~> Model
fun updt(msg, model) {
  switch (msg) { case UpdateBox(newStr) -> (contents = newStr) }
}
sig view : (Model) ~> HTML(Message)
fun view(model) {
  vdom
    <div>
      <form>
        <input
          type="text"
          value="{model.contents}"
          e:onInput="{fun (str) { UpdateBox(str) }}" />
        <div>{textNode(reverseString(model.contents))}</div>
      </form>
    </div>
}

fun mainPage() {
  page
    <html>
      <body>
        {| mvuPage((contents=""), view, updt) |}
      </body>
    </html>
}

serveThis(mainPage)
